; Waltham pedal
; 00 New Delay
; Clean 1 second delay program

; Controls:
; Pot 0: Feedback amount or reverb time
; Pot 1: Tone control
; Pot 2: Delay time or reverb size

; Memory declarations

  MEM tap1 1
  MEM tap2 1
  MEM dif1 346
  MEM dif2 568
  MEM del1 31846

; Constants

  EQU bits_12 %01111111_11111000_0000000
  EQU bits_14 %01111111_11111110_0000000
  EQU bits_16 %01111111_11111111_1000000
  EQU bits_18 %01111111_11111111_1110000

  EQU f_300Hz 0.055923
  EQU	f_1kHz	0.174549
  EQU	f_2kHz	0.318630
  EQU	f_4kHz	0.535735
  EQU	f_8kHz  0.784458
  EQU	f_12kHz 0.899931

  EQU hpf_freq f_300Hz  ; Tone control
  EQU lpf_freq f_2kHz   ; Tone control

  EQU full_scale   32767 * 256 ; 0x7FFF00
  EQU input_gain   0.25 ; 1/4
  EQU make_up_gain -2   ; 4x (when applied twice)

; All pass diffusor addresses in del1

  EQU ap_a del1 + 10
  EQU ap1  2818 + 887
  EQU ap2  2818

  EQU ap_b del1 + 5600
  EQU ap3  2446 + 933
  EQU ap4  2446

  EQU diffusion1  0.45
  EQU diffusion2  0.25
  EQU diffusion3  0.55
  EQU diffusion4  0.22

; Registers and intermediate nodes

  EQU temp_acc     reg1 ; Temporary accumulator register
  EQU	rt	         reg2 ; Delay feedback amount or reverb time (rt)
  EQU new_tap_time reg3 ; New delay tap time when moved by pot
  EQU tap_time1    reg4 ; Tap 1 time sample and hold
  EQU tap_time2    reg5 ; Tap 2 time sample and hold
  EQU xfade        reg6 ; Cross fade mix of delay taps
  EQU tap1_mul     reg7 ; Cross fade level for tap 1
  EQU tap2_mul     reg8 ; Cross fade level for tap 2
  EQU input        reg9 ; Conditioned input signal
  EQU pre_emp_hi  reg10 ; Pre-emphasis shelf state
  EQU post_emp_hi reg11 ; Post de-emphasis shelf state
  EQU lo_cut      reg12 ; DC-blocker high pass state
  EQU hpf         reg13 ; Tone control high pass filter state
  EQU lpf         reg14 ; Tone control low pass filter state
  EQU hpf_gain    reg15 ; Tone control high pass shelf gain (-0.5 to 0.0)
  EQU lpf_gain    reg16 ; Tone control low pass shelf gain (-0.5 to 0.0)

; Init program

  SKP	 run, loop
  WLDS sin0, 15, 32767   ; lfo0 is used for the cross fade signal
  WLDS sin1, 15, 200     ; lfo1 is used for chorus and diffuser mod
  WLDR rmp1, 6692, 4096  ; ramp1 is sampled for "random" values for lfo1

loop:

rt_pot:

  LDAX pot0      ; Shape pot0 for regeneration amount (rt)
  LOG	0.5, 0
  EXP	0.99, 0
  WRAX rt, 0.0

tone_pot:

  LDAX pot1               ; Tone is controlled by pot1
  SOF  1.0, -0.5          ; Shape to -0.5 - 0.5
  SKP  GEZ, set_low_gain  ;

set_high_gain:
                          ; Lower half apply a high cut
  WRAX hpf_gain, 0.0      ; Negative shelf (-0.5 - 0.0) gain on hpf
  WRAX lpf_gain, 0.0      ; The lpf shelf is flat
  SKP 0, tone_pots_done

set_low_gain:
                          ; Upper half apply a low cut
  SOF  -1.0, 0.0          ; Invert 0.0 - 0.5 to 0.0 - -0.5
  WRAX lpf_gain, 0.0      ; Negative shelf (0.0 - -0.5) on lpf
  WRAX hpf_gain, 0.0      ; The hpf shelf is flat

tone_pots_done:

; Tap times by pot2
; There are two taps, tap_time1 and tap_time2.
; Each are only updated when their part of the crossfade is low.

set_new_tap_time:

  RDAX pot2, 1.0          ; Load
  SOF 0.94,  0.05         ; Scale to del1
  WRAX new_tap_time, 0.0  ; Store

; Get LFO values
; Rectify the sin and cos signals to get a pair of xfade curves

  CHO RDAL, sin0          ; Read sin0 value
  SOF 0.5, 0.5            ; Scale lfo (-1 to 1) to 0 to 1
  WRAX tap1_mul, 1.0      ; The tap 1 level stored in tap1_mul
  SOF -1.0, 0.999         ; Invert the lfo for the other side of the xfade
  WRAX tap2_mul, 0.0      ; The tap 2 level stored in tap2_mul

; Update the tap times when the xfade level is low,
; otherwise skip and leave the tap where it is.

check_tap_1:
  LDAX tap1_mul
  SOF  1.0, -0.001
  SKP  GEZ, check_tap_2

update_tap_1:
  LDAX new_tap_time
  WRAX tap_time1, 0.0

update_lfo1_rate:
  CHO RDAL, rmp1       ; obtain a quasi random value
  SOF  0.05, 0.07      ; scale it
  WRAX SIN1_RATE, 0.0  ; sin1 rate only updated infrequently

check_tap_2:
  LDAX tap2_mul
  SOF 1.0, -0.001
  SKP GEZ, taps_done

update_tap_2:
  LDAX new_tap_time
  WRAX tap_time2, 0.0

update_lfo0_rate:

  CHO RDAL, rmp1
  RDAX pot2, 0.5
  SOF  -0.2, 0.25
  WRAX SIN0_RATE, 0.0

taps_done:

; Scale crossfade frequency with delay time

read_tap_1:
  CLR
  OR   full_scale          ; Load ACC with 0x7FFF00
  MULX tap_time1           ; Scale by the tap position
  WRAX addr_ptr, 0         ; Set addr_ptr register for tap time
  RMPA 1.0                 ; Read delay memory
  MULX tap1_mul            ; Scale amplitude for xfade curve
  WRA  tap1, 0.0           ; Write

read_tap_2:

  OR   full_scale          ; Load ACC with 0x7FFF00
  MULX tap_time2           ; Scale by the tap position
  WRAX addr_ptr, 0         ; Set addr_ptr register for tap time
  RMPA 1.0                 ; Read delay memory
  MULX tap2_mul            ; Scale amplitude for xfade curve
                           ; Tap 2 in ACC

add_taps:

  RDA  tap1,  1.0          ; Add in tap1
  WRAX xfade, 1.0          ; Save the xfade result

; Output to DAC

  SOF make_up_gain, 0.0    ; Scale up for make-up gain
  SOF make_up_gain, 0.0
  WRAX dacl, 0.0           ; Write to dac left output

; Input conditioning and bit-depth reduction

  RDAX adcl, 1.0   ; Left input
  SOF input_gain, 0.0
  WRAX input, 0.0

; Process feedback with filters and combine with input

  RDAX xfade, 1.0          ; Read output xfade back in for feedback

lo_cut_filter:

  RDFX lo_cut, 0.01
  WRHX lo_cut, -1.0

hi_shelf_tone_control:

  WRAX temp_acc, 1.0
  RDFX hpf, hpf_freq
  WRHX hpf, -1.0
  MULX hpf_gain
  RDAX temp_acc, 1.0

lo_shelf_tone_control:

  WRAX temp_acc, 1.0
  RDFX lpf, lpf_freq
  WRLX lpf, -1.0
  MULX lpf_gain
  RDAX temp_acc, 1.0

; Scale feedback for rt and then add the input signal

  MULX rt
  RDAX input, 1.0

; Feedback processing complete, write to the delay memory

  WRA del1, 0.0
