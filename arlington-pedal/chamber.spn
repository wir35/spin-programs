; Chamber-style reverb stereo in -> stereo out
; github.com/wir35/spin-programs

; Controls:
; Pot 0: Reverb decay time, short to long
; Pot 1: Density amount, low to high
; Pot 2: Room size, small to large

; Topology:
; 8 delay lines, the two shortest are modulated
; Single summing junction with slight high and low dampening, feedback to all delay lines
; Left and right output taps processed separately through chains of diffusers 
; Room size affects the energy level of the longest delay lines and the level of the distant taps
; Density affects the energy level of the shortest delay lines
; Distant taps are passed through low pass filters

; Main delay lines
  MEM del1 1597
  MEM del2 1489
  MEM del3 2279
  MEM del4 2384
  MEM del5 3133
  MEM del6 2951
  MEM del7 6817
  MEM del8 6359

; Left side output diffusers
  MEM apl1 239 
  MEM apl2 331
  MEM apl3 269
  MEM apl4 347
  MEM apl5 283
  MEM apl6 353
  MEM apl7 433

; Right side output diffusers
  MEM apr1 479
  MEM apr2 337
  MEM apr3 293
  MEM apr4 359
  MEM apr5 271
  MEM apr6 349
  MEM apr7 241

; Registers

  EQU junction reg1 ; Sum at scattering junction

  EQU lpf1 reg2 ; Low pass filters
  EQU lpf2 reg3
  EQU lpf3 reg4
  EQU lpf4 reg5
  
  EQU damping_hi reg6 ; High shelf damping at the scattering junction
  EQU damping_lo reg7 ; Low shelf damping at the scattering junction

  EQU rt reg8   ; Reverb time coefficient
  EQU size reg9 ; Room size coefficient
  EQU density reg10 ; Density coefficient

  EQU delay1_out reg14 ; Temp storage for modulated reads at end of delay lines
  EQU delay2_out reg15

; Constants

  EQU f_50Hz  0.01
  EQU f_300Hz 0.055923
  EQU f_1kHz  0.174549
  EQU f_2kHz  0.318630
  EQU f_4kHz  0.535735
  EQU f_8kHz  0.784458
  EQU f_12kHz 0.899931

  EQU rt_pot  pot0  ; Reverb time control
  EQU density_pot  pot1 ; Density control
  EQU size_pot pot2  ; Room size control

SKP run, loop

lfo_setup:
  WLDS sin0, 17, 97
  WLDS sin1, 19, 80
  
loop:

scale_pots:
  LDAX rt_pot   ; rt
  SOF 0.59, 0.4
  WRAX rt, 0.0
  LDAX size_pot ; size
  SOF 0.7, 0.3
  WRAX size, 0.0
  LDAX density_pot ; density
  SOF 0.6, 0.4
  WRAX density, 0.0

compute_junction:
  RDAX delay1_out, 0.25 ; read the end of all delay lines
  RDAX delay2_out, 0.25
  RDA  del3#, 0.25
  RDA  del4#, 0.25
  RDA  del5#, 0.25
  RDA  del6#, 0.25
  RDA  del7#, 0.25
  RDA  del8#, 0.25

  RDAX damping_hi,  f_1kHz  ; Apply some high frequency damping
  WRHX damping_hi, -0.2
  RDAX damping_lo,  f_50Hz  ; Apply low frequency damping
  WRLX damping_lo, -0.2
  WRAX junction, 0.0        ; Write the junction result

delay1:
  CHO RDA, sin1, SIN | REG | COMPC, del1# - 100  ; Chorused read at end of delay
  CHO RDA, sin1, 0, del1# - 99
  MULX density              ; Energy loss for density level
  WRAX delay1_out, -1.0     ; Save it for junction sum, and invert

  RDAX junction, 1.0    ; Add the "reflected" energy from the junction
  MULX rt               ; Energy loss for reverb time control
  RDAX adcl, 0.25       ; Add the left input signal
  WRA  del1, 0.0        ; Write to delay 1

delay2:
  CHO RDA, sin0, SIN | REG | COMPC, del2# - 100
  CHO RDA, sin0, 0, del2# - 99
  MULX density
  WRAX delay2_out, -1.0

  RDAX junction, 1.0
  MULX rt
  RDAX adcr, 0.25  ; Right input
  WRA  del2, 0.0

delay3:
  RDA  del3#, -1.0
  RDAX junction, 1.0
  MULX rt
  RDAX adcl, 0.25 ; Left input
  WRA  del3, 0.0

delay4:
  RDA  del4#, -1.0 
  RDAX junction, 1.0
  MULX rt
  RDAX adcr, 0.25 ; Right input
  WRA  del4, 0.0

delay5:
  RDA  del5#, -1.0     
  RDAX junction, 1.0 
  MULX rt 
  RDAX adcl, 0.25  ; Left input
  WRA  del5, 0.0 

delay6:
  RDA  del6#, -1.0
  RDAX junction, 1.0
  MULX rt
  RDAX adcr, 0.25 ; Right input
  WRA  del6, 0.0

delay7:
  RDA  del7#, -1.0     
  RDAX junction, 1.0
  MULX rt
  RDAX adcl, 0.25 ; Left input
  MULX size   ; Additional energy loss scaled by size control
  WRA  del7, 0.0

delay8:
  RDA  del8#, -1.0 
  RDAX junction, 1.0
  MULX rt
  RDAX adcr, 0.25 ; Right input
  MULX size  ; Size control
  WRA  del8, 0.0

left_output:

; distant tap
  RDA  del7#,  -1.0
  RDA  apl1#, -0.75
  WRAP apl1,   0.75
  RDA  apl2#,  0.65
  WRAP apl2,  -0.65
  RDFX lpf1, f_2kHz
  WRAX lpf1, 1.0
  MULX size

; distant tap
  RDA  del5#, 1.0
  RDA  apl3#,  0.5
  WRAP apl3,  -0.5
  RDA  apl4#,  0.5
  WRAP apl4,  -0.5
  RDFX lpf2, f_4kHz
  WRAX lpf2, 1.0
  MULX rt
  MULX size

; middle tap
  RDA  del3#,  -1.0
  RDA  apl5#,  0.5
  WRAP apl5,  -0.5
  MULX rt

; close tap
  RDA del1 + 312, 1.0
  RDA  apl6#,  0.25
  WRAP apl6,  -0.25
  RDA  apl7#,  0.25
  WRAP apl7,  -0.25

  WRAX dacl, 0.0

right_output:

; distant tap
  RDA  del8#, -1.0
  RDA  apr1#, -0.75
  WRAP apr1,   0.75
  RDA  apr2#,  0.65
  WRAP apr2,  -0.65
  RDFX lpf3, f_2kHz
  WRAX lpf3, 1.0
  MULX size

; distant tap
  RDA  del6#, 1.0
  RDA  apr3#,  0.5
  WRAP apr3,  -0.5
  RDA  apr4#,  0.5
  WRAP apr4,  -0.5
  RDFX lpf4, f_4kHz
  WRAX lpf4, 1.0
  MULX rt
  MULX size
 
 ; middle tap
  RDA  del4#, -1.0
  RDA  apl5#, 0.5
  WRAP apl5,  -0.5
  MULX rt
 
 ; close tap
  RDA  del2 + 312, 1.0
  RDA  apr6#,  0.25
  WRAP apr6,  -0.25
  RDA  apr7#,  0.25
  WRAP apr7,  -0.25

  WRAX dacr, 0.0
